## 3.1 链接文件基础
		链接其实就是为了确定整个程序的符号地址和占用RAM、ROM的具体位置而出现的。说到链接文件，就必须提出最经典的ELF文件结构。
		ELF文件内描述了整个程序的内存架构，他是以段的形式描述的，如果你熟悉ELF文件，则能非常迅速的从ELF文件中找出全局变量在哪，局部变量在哪，程序文件在哪，下图为ELF文件的通用模型:
![[279759ee3d6d55fb68df1ab16f224f4a20a4dd08.png]]

		其中ELF header中描述了整个程序的入口地址以及机器的基本情况。
		Program header描述的是一个段在文件中的位置、大小以及它被放进内存后所在的位置和大小。
		ELF的段一般分为text，data，rodata，stack这四个段，其中text段表示代码段，data段是静态变量存储区域，rodata段是只读变量段，stack段是栈段。一般来说只需要明白这四个段就ok。
## 3.2 链接文件分析
[GNU linker](https://ftp.gnu.org/old-gnu/Manuals/ld-2.9.1/html_chapter/ld_3.html)
如果想要系统学习则推荐GNU官网，本教程仅仅是大概了解。
### 3.2.1 链接文件基本结构
		首先介绍一下链接文件的基本语法，链接文件主要分有三个部分，分别是程序的入口点entry，内存声明memory和内存结构section。
		程序的入口点entry指的是程序刚开始是从哪里运行的，比如C语言的入口点就是main。
		内存声明memory是用来表示内存的类型，开始地址和内存的大小的。
		内存结构section则是用来指定最后生成的ELF文件具体的每个段的位置和结构的。

### 3.2.2 链接文件基本语法
```ld
	ENTRY(symbol)
```
	entry()是一个函数，内部需要一个symbol作为参数，表示从哪个符号开始进入运行程序。

```ld
MEMORY
{
RAM (xrw)      : ORIGIN = 0x20000000, LENGTH = 20K
FLASH (rx)      : ORIGIN = 0x8000000, LENGTH = 64K
}
```
	memory的结构语法如下，RAM(xrw)种括号内表示的是内存的属性，分别是
		x:包含可执行代码部分
		r:可读的
		w:可写的
	后边的ORIGIN表示此内存段的起始地址，LENGTH表示内存段的长度。

```ld
SECTION
{
	symbol:
	{
		.=.+4;
	}>RAM
	......
}
```
	SECTION的大概语法如下，symbol表示段符号，也可以是.text,.data，>RAM表示这一段要放入RAM内存储，也可以是>FLASH这样放入FLASH内存储,内部的"."表示的是目前地址,这个语句则表示地址加4，也就是空出了4个字节的空间。SECTION中每个段是按照顺序放入对应存储内存的，所以如果有限段需要在特定的位置，则需要注意！(比如中断向量表)。
	在SECTION段中也可以设置全局符号如:
```ld
.text :
  {
    _etext = .;        /* define a global symbols at end of code */
  } >FLASH
```
	在内部设置的全局符号是可以被调用的比如在C语言内使用
```C
	int *symbol=&_etext;
```
	就可以取出_etext的值。剩下的语法都是一些函数了，就不再讲解了，直接分析文件吧！

### 3.2.3链接文件源码分析
		给出关键部分的注释，其实文件并没有什么难度。
```ld
ENTRY(Reset_Handler)    /* 设置入口位置 */
_estack = 0x20005000;    /* 设置栈的结束位置 */

_Min_Heap_Size = 0x200;      /* 设置堆大小  */

_Min_Stack_Size = 0x400; /* 设置栈大小 */

  

/* 设置内存 */
MEMORY
{
RAM (xrw)      : ORIGIN = 0x20000000, LENGTH = 20K
FLASH (rx)      : ORIGIN = 0x8000000, LENGTH = 64K
}

  

/* 设置段的具体位置 */
SECTIONS
{
  /* isr_vector是ARM的异常向量地址段，优先放入FLASH内实现内存绑定 */
  .isr_vector :
  {
    . = ALIGN(4);    /* ALIGN(4)表示四字节对齐，这是由于ARM要求指令集要这么对齐 */
    KEEP(*(.isr_vector)) 
    . = ALIGN(4);
  } >FLASH
  
  /* 接着把代码段放入FLASH */
  .text :
  {
    . = ALIGN(4);
    *(.text)           /* .text sections (code) */
    *(.text*)          /* .text* sections (code) */
    *(.glue_7)         /* glue arm to thumb code */
    *(.glue_7t)        /* glue thumb to arm code */
    *(.eh_frame)
    KEEP (*(.init))
    KEEP (*(.fini))
    . = ALIGN(4);
    _etext = .;        /* 定义一个全局符号记录代码的结束位置 */
  } >FLASH

  
  /* Constant data goes into FLASH */
  .rodata :
  {
    . = ALIGN(4);
    *(.rodata)         /* .rodata sections (constants, strings, etc.) */
    *(.rodata*)        /* .rodata* sections (constants, strings, etc.) */
    . = ALIGN(4);
  } >FLASH

  
/* 这个玩意说是把外部符号表加入，其实删了也没事 */
  .ARM.extab   : { *(.ARM.extab* .gnu.linkonce.armextab.*) } >FLASH

  .ARM : {
    __exidx_start = .;
    *(.ARM.exidx*)
    __exidx_end = .;
  } >FLASH

  

  .preinit_array     :
  {
    PROVIDE_HIDDEN (__preinit_array_start = .); /* 这个设置了一个不可见符号 */
    KEEP (*(.preinit_array*))
    PROVIDE_HIDDEN (__preinit_array_end = .);
  } >FLASH

  .init_array :
  {
    PROVIDE_HIDDEN (__init_array_start = .);
    KEEP (*(SORT(.init_array.*)))
    KEEP (*(.init_array*))
    PROVIDE_HIDDEN (__init_array_end = .); 
  } >FLASH

  .fini_array :
  {
    POVIDE_HIDDEN (__fini_array_start = .);
    KEEP (*(SORT(.fini_array.*)))
    KEEP (*(.fini_array*))
    PROVIDE_HIDDEN (__fini_array_end = .);
  } >FLASH

  

  /* 这里需要注意，这部分设置用来吧FLASH中的全局变量放入RAM中，也就是C语言初始化 */
  _sidata = LOADADDR(.data);
  /* LOADADDR(.data) 返回在FLASH中.data所在的起始地址，也就是加载的起始地址 */

  .data :
  {
    . = ALIGN(4);
    _sdata = .;        /* create a global symbol at data start */
    *(.data)           /* .data sections */
    *(.data*)          /* .data* sections */
    . = ALIGN(4);
    _edata = .;        /* define a global symbol at data end */

  } >RAM AT> FLASH  /* 这里表示存储在FLASH中但是需要被搬移到RAM中*/

  

  /* Uninitialized data section */

  . = ALIGN(4);
  .bss :
  {
    /* This is used by the startup in order to initialize the .bss secion */
    _sbss = .;         /* define a global symbol at bss start */
    __bss_start__ = _sbss;
    *(.bss)
    *(.bss*)
    *(COMMON)
    . = ALIGN(4);
    _ebss = .;         /* define a global symbol at bss end */
    __bss_end__ = _ebss;
  } >RAM

  

  /* User_heap_stack section, used to check that there is enough RAM left */

  ._user_heap_stack :
  {
    . = ALIGN(8);
    PROVIDE ( end = . );
    PROVIDE ( _end = . );
    . = . + _Min_Heap_Size;
    . = . + _Min_Stack_Size;
    . = ALIGN(8);
  } >RAM

  

  

  /* Remove information from the standard libraries */

  /DISCARD/ :

  {
    libc.a ( * )
    libm.a ( * )
    libgcc.a ( * )
  }
  .ARM.attributes 0 : { *(.ARM.attributes) }

}
```

	